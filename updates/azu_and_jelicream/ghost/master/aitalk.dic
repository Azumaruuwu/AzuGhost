//---------------------------AI Talk--------------------------
//Alright, this is it! The bread and butter of your ghost. This is where all their random conversations are stored. Of all the .dic files, this is the one you will probably be adding stuff to and updating the most as new dialogue comes to your mind. It is relatively simple in that there aren't many functions or real coding involved with this file, but there are a few cool tricks you can do within randomtalk that can make dialogue with interesting effects, so I'll point them out as we go along.

//I'm going to leave a lot of examples of different types of dialogue in here that you can study if the possibilities they present intrigue you. Study them carefully!


//Make sure to read the walkthrough (http://www.ashido.com/ukagaka/) for more details about how to properly format ghost dialogue!
//I will reiterate that you should be editing these in Notepad++, and in particular, you should set the Language to C (or Java I guess) to properly highlight all the text. It will make things A LOT EASIER for you, trust me! It will also help you keep track of your brackets in case you forget some. If you click on a line with a bracket in Notepad++, it should highlight where the other bracket ends, or turn purple if there's no finishing bracket (which you should definitely fix!)
//It will also highlight my commented lines in green, so you'll know where they are. Any line beginning with // is a commented line that the code will not read. It's just for your reference!


//If you see a big block of commented text you want to uncomment, highlight the text and then hit ctrl-shift-k in Notepad++ and that should uncomment it. Mostly this is intended for advanced users.

/*
This file has...

AiTalk
RandomTalk
any chains you set up
HourCheck
OnMinuteChange
OnSecondChange
OffscreenTalk
EndOffscreenTalk
OnSurfaceRestore
OnKeyPress
*/


//--OnAiTalk
//Leave this alone. This is mostly setting up whether or not dialogue is in a chain before it displays.

OnAiTalk
 {
	if RAND(100) < communicateratio
 	{
 		StartCommunicate
	}
 	else
	{
			if CHAIN.IDName == "" {
 				lastTalk = RandomTalk
			}
			else {
				lastTalk = ChainTalk
			}
			lastTalk
	}
 }
 
 
 //*********************Advanced User Info*******************************
//These text boxes are meant for those who have an interest in expanding or using more complicated functions in their ghost. Also for my own reference in the future since I plan on using this template too, haha. If you have no interest in any of this and just want to keep things as simple as possible, feel free to skip these boxes.

//--Mikiretalk, or the Off-Screen Boundary Check--

//This has been a feature in the template for a very long time, and while it's enjoyable enough for some, for others it can be rather irritating or goes against what their ghost is. So to that end, I'm making this an opt-in feature. Basically, this flag checks to see if the ghost is off the screen and haves them say something to the user to get them to move them back on screen. If you want to enable this, it'll take a bit of fussing around. You're going to want to add the mikireflag check into OnAiTalk above. You can see how so below. Then you'll have to follow up on the flag check later down at OnSecondChange. Keep an eye out.

 
//
// OnAiTalk
 // {
	// if RAND(100) < communicateratio
 	// {
 		// StartCommunicate
	// }
 	// else
	// {
 		// if mikireflag == 0
		// {
			// if CHAIN.IDName == "" {
 				// lastTalk = RandomTalk
			// }
			// else {
				// lastTalk = ChainTalk
			// }
			// lastTalk
		// }
 		// else
		// { 
		// MikireTalk
 		// }
	// }
 // }
//***********************************************************************


//--RandomTalk 
 //Alright, here we go! This is where ALL your ghost's random dialogue will go! These are the conversations that they'll have with you or each other that pop up depending on the talk rate defined in menu.dic (or if you ignored that, set in bootend.dic in the first boot, which would be every five minutes).
 
 
//*********************Advanced User Info*******************************
//These text boxes are meant for those who have an interest in expanding or using more complicated functions in their ghost. Also for my own reference in the future since I plan on using this template too, haha. If you have no interest in any of this and just want to keep things as simple as possible, feel free to skip these boxes.

//You CAN link to other pools of dialogue in RandomTalk using envelopes/functions, such as %(teatalk) to pull from a pool of dialogue about tea. Then you'd just add that line in among the other dialogue in here with %(teatalk).

//You can't do things like put OnSystemBoot or something in RandomTalk. You could use \![raise] to pull something though, although I'd be careful about it. More on these kind of things and conditionals in general in the coding page on the walkthrough.

//If you split up RandomTalk with if/else checks, then you can redirect in an If to another function, like... if chargermode == "on" { ChargerChatting } else {normal random talk dialogue}. This would redirect the random dialogue to ChargerChatting instead. You can't just throw a function into RandomTalk directly though, like "dialogue 1" "dialogue 2" OnFunctionname "dialogue 3", that kind of thing. You'll have to split it off into its own if or point to it with an envelope.

//You can do multiple ifs in RandomTalk if required... like an if for if a character is asleep, or if a third character is present, or if they're having a nervous breakdown, or if they're sulking. It depends! You can add a bunch of checks leading to specialized pools if you want. Just make sure you leave some normal dialogue in the last else for RandomTalk (unless you want to redirect that somewhere else with an envelope too... although that seems a bit redundant, honestly.)
//***********************************************************************


//nonoverlap_pool should make it so all your dialogue has an equal probability of being called, and it will do each line of dialogue before repeating them. "Why" is kind of technical and a bit outside the scope of this template, you can look into why elsewhere if you want.

RandomTalk : nonoverlap_pool
{
	//Alright, here is where all your dialogue will go. I hope you studied how to format dialogue carefully in the walkthrough, as there is little here I can help you with. You're going to have to write most of it yourself. So instead, I'll give you a set of examples to look at and perhaps draw inspiration from! You don't have to use any of these of course, you can just wipe everything in RandomTalk and just write your own dialogue by scratch. But if you're new, I'm hoping these will serve as some little stepping stools as you get more used to writing and coding dialogue.
	
	"\0\s[6]I'm trying to set up a stand! \w8\s[4]It's been tough so far... \w8\s[5]but my Momma didn't raise a dummy!\w8\1\s[15]You're right! \w4\s[18]She raised a \f[bold,true]psycho\f[bold,false]. \w8\0\n\n[half]\s[7]Hey! \w8\s[9] That's not true...\e"
	
	"\0\s[4]I'm feeling kinda down...\w8\1\s[15]Aww, don't be sad! There's hope for everyone! \w8\0\n\s[5] Wow, even me?!\w8\w6\1\n\s[16] No.\w8\0\n\n[half]\s[9] Hmph...\e"
	
	//Single lines of dialogue like this are simple and easy to do.
	
	"\0\s[6]I used to have a pet fish!\w8\1\s[18]Used to? \w8What happened to it?\w2\s[10]\w6\0\n\n\s[3]... \n\w8They looked too tasty...\w8\s[4] I had no choice...\w8\1\n\n\s[19]... \w8What else is new.\e"
	
	"\0\s[8]Staying inside is good for anxiety, \w4but not depression. \w4\nand going out is good for depression, \w4but not anxiety...\w8\w6\n\n[half]\s[5] Stand directly in the doorway for peek mental health!\w8\1\s[19] Thats not how it works.\e:chain=mentalhealthchain"
	
	//Here is what a simple two line exchange between your characters might look like. Either character can start a piece of dialogue, it doesn't always have to be the main one.
	
	"\0\s[0]Why didn't the skeleton go to the party?\w8\n\n[half]Cus' he had no-\w7\s[5]\f[bold,true]body \f[bold,false]\w4to go with!\w8\1\s[19]That wasn't funny.\w8\0\s[9]\n\n[half]You could've at least played along...\e"
	
	//Note here that the code comes back to character 0 and spaces out their second statement with \n\n (or \n\n[half]... really whatever spacing looks best in your balloon). This makes it so the next line of dialogue from them is spaced apart from the previous in the balloon, which will make it easier for your user to read.
	
	"\1\s[13]Ughh.. my landlord's probably gonna evict me soon.\w8\0\s[7]I dont wanna hear that negative attitude! \w4\s[5]C'mon, \w2look on the bright side!\w8\1\s[19]\n\n[half]... \w8\s[15] Wooo! I'm probably gonna get evicted soon!\w8\0\s[6]\n\n[half]Much better.\w8\1\s[19]\w6\n\n[half]Hah.\e"
	
	//As you can see, you can extend a conversation as long as you want by remembering to space things with \n\n as needed.
	
	"\0\s[5]Hehe, \w4this one time, I tripped an old lady walking down the street!\w4\w8\1\s[19]You have a serious problem.\w8\0\n\s[6] You call it a problem, \w4I call it a solution!\w8\1\n\s[14]A solution to what?? \w8\0\n\n[half]\s[6]Old people.\e"
	
	//Note here that s[0] has become s[1] and s[10] has become s[11]. As I explained in the walkthrough, the \s[] tag determines the pose of your ghost. Poses are defined in surfaces.txt within your shell's folder. If you're following my guidelines for your ghost, you should have around ten poses for each character. You can change the \s[] value depending on what expression you want your ghost to have.
	
	"\1\s[18]I could go for a buck right about now...\w8\0\s[7]Jelicream! \1\s[10]\0\s[7]\w4Money can't make you happy!\w8\1\n\s[17]Well it sure as heck isn't gonna make me sad!\w8\0\n\n[half]\s[1]\f[height,20]GASP!!\f[height,default] \w4Jelicream you can't say that!\w8\1\n\n[half]\s[12]Wha- \w6\s[17]I didn't curse??\w8\0\n\s[7]Say sorry right now!\w8\1\n\s[19]... \w8Sorry.\e"
	
	//As you can see here, you can change your ghost's expression while they are speaking. This can make their expression vary dynamically depending on what they're talking about.
	
	"\0\s[7]Remember, %(username)!\w4\n\n\s[0]If someone's real mean to you, \w2\s[5]find their house and steal all their left socks!\w8\1\s[19]You're crazy.\w8\0\n\n[half]\s[6]Crazy-creative you mean!\e"
	
	//You can also change the side character's expression while the other character is talking. Note here that after the surprising statement, Triangle's pose is changed to s[12]. Then the focus is brought back to Girl with the \0 tag and she resumes talking. You can change focus without having the character actually say anything to accomplish just this effect.
	
	"\1\s[18]Have you had anything to eat today, %(username)?\w8\0\s[2]Ooooh, \w2\s[5]yea! \w4Like candy!\w8\1\s[19]\nNot like candy.\w5\0\nI looooveeeee candy! \w4\s[0]Especially the really sweet ones,\w2\1\s[10]\0\ like jawbreakers.\w4\s[3]\w8\n\nThough... the last time i ate one, I got a really bad \_a[https://www.youtube.com/watch?v=jF3gbuxXoQk]toothache\_a. \w8\1\s[16]\n\nProbably because you tried to bite straight through it.\w6\0\s[9]\e"
	
	"\0\s[2]Oh\w1 my\w1 God\w1, Jelicream!\w2\1\s[10]\0 I have a good one!!! \w8\s[8]\n\nWhy shouldn't you invest in time travel?\w8\w8\1... \w4Why?\0\w4\s[5] Cus' there's no \w4\f[bold,true]future\f[bold,false]\w4 in it!\w8\1\s[19]\n\n I think that was the worst one yet.\w2\0\s[1]\n\nW-\w2What?! \w4\s[7]No, it's really clever!!\w4\1\s[18]\n\nWhatever you say.\e"
	
	"\0\s[1]\f[bold,true]O\w4M\w4G\w4 JELICREAM.\f[bold,false]\w4 I just had the craziest dream last night!\w8\1\s[18]Uh huh?\w8\1\s[10]\0\s[5]\n\nYeah, I was in a whole ocean of orange soda!\w8\w8\s[4]\nBut, \w8it turns out...\w8\w8\n\nIt was just... \s[22]a \f[bold,true]Fanta\w8-\f[bold,true]sea!\f[bold,false]\w8\1\s[19]\n\n...\w8Really now?\e"
	
	"\0\s[2]Jelicream!!!\1\s[10]\0\w5\s[6]\n\nWhat do you call an alligator in a vest?\w9\w9\1\... an investig-?\s[19]\0\s[22]\n\n\f[bold,true]AN INVESTIGATOR!!!!!\f[bold,false]\e"
	
	"\0\s[2]Oh my god,\w4 Jelicream!!\w8\1\s[10]What?\w8\0\s[3]\nDid you hear about the actuary who wouldn't make whipped cream?\w8\1\s[51]\w4\n\nUuuhhhh...\w8 no?\w8\0\s[2]\n\nYeah!!\w4\1\s[10]\0\w4\nThey say she was...\w8\s[22] \f[bold,true]whisk\f[bold,false]\w8-averse!\w8\1\s[19]\w8\w7\n\n... Of course.\e"
	
	"\1\s[19]Y'know, I swear that weird hat\0\s[0]\1 of yours was it's own thing.\w8\0\s[5]Pff-\w8\s[0] what?\w8\1\s[18]\n\n[half]No,\w2 really,\w4 I remember it-\s[10]\0\s[3]\n\nUhh-\w2 Seriously,\w2 what are you talking about?\w5\s[9]\n\n[half]I think it'd be even more creepy if it was,\w2 like,\w2\s[5] sentient or something,\w5 heh.\w8\1\s[13]\n\n...\w9\w9\w9\w9\s[19]\n\n[half]Ughhhhh...\w6whatever.\e:chain=weirdhatchain"
	
	//"\0\s[8]This is my thoughtful pose.\w8\1\s[18]Where did we put that thing?\e"
	
	"\0\s[3]\w9\w8...\w8\nJelic\1\s[10]\0ream?\w8\s[0]\nWhat if a cartoon said fart?\w4\1\s[19]No.\w4\0\s[2]\nNo, no, no hear me out!\w4\1\s[10]\0\s[5]\n\nWhat if a cartoon-\1\nNo-\0\nkinda stumbled over his words but we kept it in-\1\nNo y-\1\s[19]\0\s[9]\n\nLet me finish Jeli!\w8\n\s[0]What if...\w4 a cartoon said big farts.\w8\1\s[18]\n\nNo,\w3 you're just repeating yourself.\w8\0\s[6]\n\nOkay, okay.\w8\nWhat if...\1\s[19]\w8\w5\n\nUh huh?\w8\w5\0 a cartoon...\w8\w9\1\n...Yeah?\w8\0 said big farts.\w8\1\s[18]\n\nIt's just repetition,\w4 you're using the rythyms of comedy with no punchline.\w8\0\s[9]\n\nWhat do you know?\n\w4 You've never made a joke in your life.\w8\1\s[19]\n\nWhat about when I'm a victim of meaningless violence?\e"
	
	//Here's dialogue with all of their poses so you can see them in action. Note how the \s number changes for each of them. They don't have to match of course, you can have one character happy and one character sad and do whatever you like. These are just examples you can rewrite as you like with your own dialogue.
	
	"\1\s[13]Ughh, my neighbours are so loud. \w8\0\s[2]Really? \w8\1\n\s[19]Yeah, \0\s[3]\1\w2they're always complaining! \w4\n\n\s[18]Something about \_a[https://www.youtube.com/watch?v=WchseC9aKTU]two trucks\_a? \w8\n\s[19]I dunno...\e"
	
	//You can use the \w tag to time your dialogue. \w goes from 1-9, although I usually leave it at 8 to be safe, and while you can't go above 9, you can layer \w tags to add up to higher numbers. Here, \w8 and \w6 combine to make 14.
	
	"\0\s[5]Hehe, \w4watch this. \w8\n\s[6]P\w6 - \w6A - \w6U - \w6S - \w6E.\w8\1\s[19]... \w8\w6\0\s[0]\1\s[19]Am I supossed to say something? \w8\0\n\n[half]\s[2] Uh- \w4\s[6]Yes!!\w8\1\n\s[19]Sure.\e"
	
	//You can also pause multiple times within sentences, like so. At the end, you can pause between letters if your ghost is spelling something.
	
	//"\1\s[10]Here is dialogue on one line.\nHere is dialogue on the next line.\e"
	
	//You can use \n to move dialogue down one line, like hitting enter once. 
	
	//"\0\s[0]This is dialogue referring to %(username).\w8\1\s[10]Hello, %(username).\e"
	
	//Here, the characters are using the %(username) envelope to use the user's name. More on envelopes in word.dic. You can use any envelope you want in dialogue, practically. Note that if your user hasn't set their username, they will be referred to as USER. You can also use %username (with no parentheses) to grab the user's name directly from SSP itself rather than have them tell your ghost specifically.
	
	//"\1\s[10]Here is dialogue using %(username)'s pronouns.\w8\0\s[0]%(heshe) went to the store and got %(himher)self some milk for %(hisher) cat.\e"
	
	//You can use the pronouns set up during their first boot (see bootend.dic) or in the config menu (menu.dic) in sentences like this as well. You can see that the envelope %(heshe) replaces where he or she would be in the sentence, and so on for the rest of them. This makes it so they use the correct pronoun in the right place. If you don't have pronouns enabled on your ghost, you don't need to worry about these envelopes and shouldn't use them.
	
	//"\1\s[10]Here we'll use the envelope 'exampleenvelope' to show what it looks like in a balloon.\w8\0\s[0]%(exampleenvelope).\e"
	
	//As you can see, you can put any envelope from word.dic in here, as long as you properly format it as an envelope by putting a percent sign in front and putting it in parentheses. %(username), %(icecream), %(bunnies), whatever, this is up to you as long as you've defined it in word.dic. You can see how the values stored in the envelope appear in the balloon when the dialogue is called.
	//Do note that you cannot put quotation marks in your dialogue. If you're using Notepad++ which you should be doing, you'll see the line break and turn black if you do. Use apostrophes instead.
	
	//"\1\s[10]Here we'll use the envelope 'blanklist' to show what it looks like in a balloon.\w8\0\s[0]%(blanklist).\e"
	
	//"\1\s[10]Here we'll use the envelope 'blanklist2' to show what it looks like in a balloon.\w8\0\s[0]%(blanklist2).\e"
	
	//"\1\s[10]Here we'll use the envelope 'blanklist3' to show what it looks like in a balloon.\w8\0\s[0]%(blanklist3).\e"
	
	//And here are more examples using the blanklists I set up in word.dic.
	
	//"\1\s[10]Here we'll use the envelope 'usersfolder' to pretend to look at the user's files.\w8\0\s[0]Hey, have you looked at %(usersfolder) lately?\w8\1\s[19]\n\n[half]No.\e"
	
	//You can use the usersfolder envelope creatively if you like, haha.
	
	//"\1\s[10]Here we'll use the envelope 'internalenvelope' to show an envelop containing another envelope.\w8\0\s[0]%(internalenvelope).\e"
	
	//Here's an example using the internal envelope I mentioned in word.dic. Basically, a function within a function. More on that kind of thing on the coding page of the walkthrough.
	
	//"\1\s[10]Here is dialogue referring to Anchor 1.\w8\0\s[0]Here is dialogue referring to Anchor 2 and Anchor 3.\e"
	
	//Anchor words are defined more in anchor.dic. Note here when you see this dialogue that Anchor 1, 2, and 3 become links when spoken, even if they weren't written as links in the dialogue in this file. This is why anchor words are convenient, you don't have to set up link code every time they say a certain word. Try clicking the links when they appear to see what they lead to. It'll give you an idea of exactly how anchors work.
	
	
	//--Chains
	//Chain dialogue is unique. A dialogue chain consists of multiple bits of dialogue that get called in order over a period of time.
	
	//"\0\s[0]This is the first part of a chained dialogue.\w8\1\s[10]Second part to come when we next speak.\e:chain=achain"
	
	//Note here that instead of ending the dialogue with the normal end tag of \e, it ends with \e:chain=achain. This will link down below to a chunk of code outside RandomTalk called achain. You can scroll down and find it now if you want. You can name your chain anything you want, it'd just follow the same format of \e:chain=yourchainsname. Once a dialogue has entered a chain, they won't say any other random dialogue until the chain is finished.
	
	//"\0\s[0]This will lead to the second piece of chained dialogue.\w8\1\s[10]This will have another chain attached to it.\e:chain=anotherchain"
	"\0\s[5]Hehe, here's a \_a[https://www.youtube.com/watch?v=Wo8yhLMemGk]link!\_a \e:chain=rickrollchain"
	
	"\0\s[8]So, wait. \w4If we're on a computer.. \w8\s[5]does that mean we're technicallly on the internet too?\w8\1\s[15]What brought this up?\w8\0\n\n[half]\s[2]Oh! \w4\s[6]\_a[https://www.youtube.com/watch?v=k1BneeJTDcU]Nothing...\_a  \e:chain=internetchain"
	//Here, this will link to a different chain to continue the conversation. You can scroll down to the section on chains to see this in action.
	
	
	//--If statements
	//You can put if statements in RandomTalk! They don't even need Else statements, unless you want to keep two sets of statements completely separate - for example, you have one pool of conversations for when there are two characters, and one for if there are three characters. You wouldn't want these to overlap, so you'd put one pool in the if statement and the other in the else. Most of the time though, you won't need an Else statement for these, since they'll mostly only be adding in dialogue if the if statement is true. 
	
	if nowshell == "old" //This checks to see what shell they're currently using. You may remember this value being defined back in bootend.dic. If they're in their default shell...
	{
	//Then these conversations are available! Note the brackets enclosing this set of dialogue.
	//"\0\s[0]Here is dialogue related to the default shell.\w8\1\s[10]You can use this to comment on clothing your ghost is wearing.\e"
	
	//"\0\s[0]Here is more dialogue related to the default shell.\e"
	}
	
	//You can also put in multiple ifs checking for the same thing to vary the responses. In this case, it's checking to see if they're wearing another shell. You don't need to use elseifs for this! Just an if is fine.
	if nowshell == "slugcat"
	{
	"\0\s[3]Y'know,\w2 as cool as it is to actually have fingers,\w4\s[9]this new height is kinda hard to get used to...\w8\1\s[16]Pff-\w2 sure.\w5 You're just as small as ever.\w8\0\s[2]\n\n[half]Wuh-\w5\n\s[7]You can't even \w1\f[italic,true]make\f[italic,false]\w1 that joke, you're the same size as me!!\w8\1\s[18]\n\nKeep hoping.\w4\0\n\n\f[height,15]WHATEVER!!!!!!!!\f[height,default]\w8 \s[9]\n\n[half]\f[bold,true]UGHHHH.\f[bold,false]\e"
	
	}
	//After the if statement is done, you can continue with dialogue as normal. As you can see, you can move the ifs around in here, it doesn't really matter. You may want to stick them all in one place together if you want to keep track of them, or you can scatter them around your dialogue as you please. It's up to you. These two lines below are just normal random dialogue.
	
	"\0\s[0]Jelicream... \w8\w4\n\s[5]Hey \1\s[19]\0\Jelicream.\w9\w4\0\s[0]\w9\n\n\s[5]Jelicreeeeeeaammmmm~\1\s[17]\f[height,20]\f[bold,true]WHAT.\f[height,default]\f[bold,false]\0\s[0]\w9\1\s[19]\w9\w6\0\s[5]\n\n...\w8 amogus\w4\1\s[10]\n\n... \w8\s[14]Kill me.\e:chain=kisschain"
	
	//"\1\s[10]This is more generic dialogue to be replaced.\w8\0\s[0]Do whatever you like with it.\e"
	
	//And here we're going to go back into ifs! You see, you can space out your ifs with normal dialogue if you want, as I just mentioned. Anyway, you can set up if statements to check for a lot of different things! One of the cooler ones I think are ones set to check for time. This one for example checks if the month is December. You can change month to any month you want.
	
	if month >= 12
	{
		"\0\s[0]This is dialogue for December.\w8\1\s[18]A cold month?\e"
		
		"\0\s[0]This is more dialogue for December.\w8\1\s[18]Still cold?\e"
	}
	
	//So you can use these to check for any number of situations. You can add as many or as few if checks if you want. You can delete these if checks too if you don't want them.
	//You can also nest ifs within these, so you could check if it was the 25 of April in the morning for example. You can also add or take away dialogue from these as you like, so if you want to have five dialogue options for April or one for January, you can totally do that.
	
	
	//This if checks what day of the week it is. You can check for any day or as many days as you want. Or none. It's up to you.
	if weekday == 1
	{
		"\0\s[5]Monday's my favourite day of the week!\w8\1\s[10]You say that about every day.\e"
		
		"\0\s[0]Oh, it's Monday!\w8\1\s[18]That it is.\e"
	}
	
	//This if checks for the time of day as defined in bootend.dic. Do you remember the words used for each chunk of time in GetTimeSlot? These are the same words! You can check for all those times, or none of them and delete this if you want.
	if timeslot == "earlymorning"
	{
		"\1\s[10]You're up early.\e"
		
		"\0\s[5]Bright and early!\e"
	}
	
	
	
	//--Double Envelopes
	//If you've gone through word.dic, you may remember a list with two sets of values on each line. This is how you would call something like that! Technically, what I call a double envelope is really an array, or a stored list of values. You can pull certain things out of that list as you need, although it requires some specific formatting. For this example, we'll use the double envelope that cindysuke made that has a Korean word and its definition. This can be a bit complicated, so pay close attention.
	//If this scares you too much, you can delete the whole thing.
	
	//note that this is enclosed in its own set of brackets
	//{
	//_korean = lessonkor //Here, the envelope "lessonkor" (which is in word.dic) is stored as a new temporary value called _korean. You can name the new value anything you like. As a reminder, when coding, it goes "4 = 2 + 2" and not "2 + 2 = 4". The result always comes first!
	//If you look at lessonkor in word.dic, you'll see it goes "Korean word, meaning". So "Korean word" is the first value in that array, and "meaning" is the second. Arrays always start at 0, as a note.
	//_korword = _korean[0] //Now we make a temporary value called "_korword", which is what will store the Korean word. Since the Korean word is the first value in the array _korean which we just defined, then we can pull that value from the array with _korean[0]. [0] is telling it which entry in the array to take - in this case, the first one (again, arrays start at 0).
	//_kormean = _korean[1] //Now we make a temporary value called "_kormean", which is what will store the meaning of the Korean word. This is the second value in the array, so we can pull it out of _korean by using _korean[1].
	
	//Now we put it all together!
	//"\1\s[10]Here is an example of a double envelope. The Korean word is %(_korword).\w8\0\s[0]The word means %(_kormean).\e"
	
	//As you can see here, we then use the values we set above for _korword and _kormean up there in the dialogue as our new envelopes. If you're planning on using a double envelope/array like this, study how this works very carefully! If you're good with arrays, you can do a lot with this kind of thing, but I must admit I am terrible with arrays and they're still hard for me to wrap my head around, so I don't have much advice on how to do that kind of thing.
	//The underscores in front of korword and kormean mean that the value is temporary, and will disappear when the dialogue has finished. More on this kind of thing on the coding page of the walkthrough.
	
	//Additional Note - You can't test this kind of dialogue in Script Input. You'll have to wait until it comes up naturally in your ghost's random dialogue.
	//}
	//That's all for double envelopes! As mentioned, if this scares you, you can delete this chunk and you'll be fine.
	
	
	//Now we're going to go back to normal dialogue for a bit of a break. Feel free to rewrite this all as you like.
	
	"\0\s[0]Hey Jelicream? \1\s[10]\0\s[8]Have you heard of Sugondeez?\w8\1\s[18]What's Sugond- \w2\s[19]oh no-\w2\0\n\n[half]\s[5]\f[height,15]SUGONDEEZ NUTS!! HAH!\f[height,default]\w8\w8\1\n\n[half]\s[13]I can't believe I fell for that...\w8\0\n\s[6]Got em'.\e"
	
	"\0\s[8]Rabbits live for 8 years, \w4Dogs live for 15 years... \w8\w8\n\n[half]\s[2]But turtles live for 150 years! \w4\s[6]Lesson learnt. \w8\1\s[10]... \w4 What's the lesson? \w8\0\n\n[half]\s[5]Stay inside and do nothing!\w4\1\n\n\s[19]of course.\e"
	
	"\0\s[3]I kinda miss my old house. \w8\1\s[15]Oh I remember, \w4The one you always fell down the stairs of?\0\n\n\s[7]Hey! I never fell!\w8\1\n\n\s[10]Then what did you do?\w8\0\nI...\w4\s[6] attacked it!\w6\1\n\n\s[19]With your face?\w8\0\n\n\s[7]I'm talented!\e:chain=glowingchain"
	
	
	//--Multiple Responses
	//If you've poked around menu.dic or string.dic or read the walkthrough, you may have seen me touch on setting multiple options for any one given bit of dialogue. This kind of dialogue follows much the same format.
	
	//{ //Again, note the brackets around these unique bits of dialogue. Dialogue that's more complicated than just a single line of "dialogue/more dialogue/more dialogue/end" usually needs to be put in brackets like this. 
	
	//"\0\s[0]I am asking you a generic question, Triangle. Generic question?\w8\1\s[10]"
	//Notice that the last few tags set up Triangle's pose before going into his text below. You don't have to do this if you want each response to have its own pose, but if he'll have the same expression regardless of what he's saying, you might as well save yourself the work. Note this does not end with \e either, since the dialogue isn't done yet.
	//-- //You've probably seen these double dashes before in other .dic files. In this case, these dashes enclose a set of multiple responses. More on double dashes in the coding page on the walkthrough site.
	//"This is my first response."
	//"This is my second response."
	//"This is my third response."
	//"This is my fourth response."
	//"This is my fifth response."
	//"This is my sixth response."
	//As you can see here, there are six potential replies Triangle might have to Girl's question. Each time this dialogue is called, he will randomly select one response from these six.
	//-- //The closing set of dashes for his dialogue options.
	//"\w8\0\s[0]\n\n[half]" //Now we're going back to Girl to set up for her multiple responses in return. This line of tags mostly just sets up for what she will say. Like above, you can see her pose is set in here with \s[0], so she'll have the same pose for each response, and the consistent spacing with \n\n[half].
	//-- //opening dashes
	//"First response to your response."
	//"Second response to your response."
	//"Third response to your response."
	//"Fourth response to your response."
	//Following the same pattern as above, Girl will randomly reply with one of these bits of dialogue. Note that these responses are also randomly chosen, so it may go Triangle says response one, Girl says response three, or Triangle says response four, Girl says response two, or so on and so forth. So make sure the exchange will still make sense regardless of what either of them might say.
	//-- //closing dashes
	//"\e" //Dialogue ends
	//}
	
	//Here's another example of multiple responses. This one will let you randomly choose something to say in the middle of a sentence.
	{
		"\0\s[5]Okay,\w2 choose a color,\w5\s[0] blue or pink?\w8\1\s[10]Uhh-\w5 "
		--
		"\s[18]blue,\w4 I guess?\w8\0\s[5]\n\n[half]\f[bold,true]AAAAAAAAWWWW\f[bold,false],\w2 because \w4\f[italic,true]I'm\f[italic,false]\w4 blue, right?\w8\1\s[10]\n\n[half]Uhh-\w4 no,\w6 I just chose at random.\w8\w8\n You're more green than blue anyways.\w6\0\s[1]\n\nWhat!?\w4\s[7] No I'm not!!!\w8\1\s[19]\n\n[half]Yea you are,\w4 you're green.\w5\0\s[7]\n\n\f[height,15]NO I'M NOT!!!!!\w4 I'M BLUE!!\w8\n\f[bold,true]I'M BLUUUUUEEEEE!!!!\f[bold,false]\f[height,default]"
		"\s[18]pink,\w4 I guess?\w5\0\s[9]\n\n[half]Ughhh,\w3 BORINGGGG.\w8\nYou just said that because\w2 \f[italic,true]you're\f[italic,false]\w2 pink.\w8\1\s[19]\n\n[half]You literally gave me two options,\w2 what else was I supposed to pick?\w8\0\s[7]\n\nBlue!!!\w6 For me!!!!\n\w8\s[9]Obviously...."
		--
		"\e"
	}
	{
	"\0\s[5]Okay,\w2 choose a color,\w5\s[0] blue or pink?\w8\1\s[10]Uhhhh... \w5" 
	-- //opening dashes
	"\s[16]green,\w2 heh.\w8\0\s[5]\n\n[half]Aaaaaawwww,\w4 'cus \w2\f[italic,true]I'm\f[italic,false]\w1 green,\w2 right?\w8\1\s[19]\n\n[half]No,\w4 because it wasn't one of the options.\w8\0\s[6]\n\nI'm going to take that as a compliment!\e"
	"\s[16]red,\w2 heh."
	"\s[16]purple,\w2 heh."
	"\s[16]orange,\w2 heh."
	"\s[16]yellow,\w2 heh."
	"\s[16]brown, heh.\w8\0\s[5]\n\n[half]Aaaaaawwww,\w4 'cus of my hat,\w2 right?\w8\1\s[19]\n\n[half]No,\w4 because it wasn't one of the options.\w8\0\s[6]\n\nI'm going to take that as a compliment!\e"//Notice here I added a tag for his thoughtful pose. If he chooses magenta, he'll do this pose. You can set up individual expressions for each potential choice this way.
	-- //closing dashes
	"\w8\0\s[2]\n\n[half]Uhh-\w2 what??\w5\s[7] That wasn't one of the options!!!\w8\1\n\n[half]\s[16]Well it is now.\w8\0\n\n[half]\s[9]Hmmph...\e" //dialogue ends
	
	//You can see that this follows the formula of the above, although there's only one set of multiple responses in here, then Triangle continues with his sentence. You can use this feature in many different ways if you're creative.
	}
	
	//"\0\s[0]\f[height,20]These words are really large.\f[height,default]\w8\1\s[10]These words are a normal size. \w8\f[height,6]These words are small.\f[height,default]\e"
	
	//You can use other fancier SakuraScript bits in your dialogue if you want to get font effects, although I won't get too much into them here. In this case, \f[height,20] changes the height of the font, in this case to 20 or to 6, and then \f[height,default] changes them back to normal. I touch more on this in the SakuraScript page of the walkthrough.
	
	//"\0\s[0]\f[strike,true]These words are struck out.\f[strike,default]\w8\1\s[10]\f[sub,true]These words are a subscript.\f[sub,false] \f[sup,true]These words are a superscript.\f[sup,false] \f[underline,true]These words are underlined.\f[underline,default] \f[bold,true]These words are bold.\f[bold,false] \f[italic,true]These words are italic.\f[italic,false] \f[color,186,146,112]These words are a color.\f[color,default] This tag would return to default.\f[default]\e"
	
	//Here's a list of similar tags you may find useful. You can see they function much like <b> or <i> tags in html. You just enclose the piece of text you want to change with them. Note that they start with \f[word,true] to turn it on, then \f[word,false] or \f[word,default] to turn it off. I assume you can tell what the tags do by the text they enclose.
	//For the \f[color] tag, the three colors listed are the RGB values of the color you want to use for your text. Change them to whatever color you want to use. \f[default] can be an easy way to quickly go back to the default word setting in your balloon after messing with colors and effects a lot.
	
	"\0\s[5]Hehe, here's a \_a[https://www.youtube.com/watch?v=dQw4w9WgXcQ]link!\_a \w8\w8\w6\1\s[19]You really think you're gonna get %(himher) with that?\w4\0\n\n[half]\s[7]Shush!\e"
	
	//Along the same lines as above, you can use \_a[address] to link to certain things. Note that it ends with \_a to close the link. It's very similar to <a href=address> and </a> in html.
	
	//"\0\s[3]\1\s[15]\_sWe are saying this dialogue at the same time.\e" 
	
	//This dialogue uses a unique tag, \_s. \_s makes it so both characters will speak the same words at the same time. Try it in Script Input to see how it works. I talk about it a little more on the walkthrough page for scripting.
	
	//And so we reach the end of randomtalk! I again reiterate, YOU CAN AND SHOULD WRITE YOUR OWN NEW LINES OF DIALOGUE FOR THIS! I included some random example phrases here for you just to replace the text and see what you can do, but there really aren't that many. You'll want to add more, and doing so is easy! Just copy and paste a line and redo the text. Repeat as many times as you want with as many conversations as you want. You can always expand this section!
}


//--Chains
//Here is where you will put your bits of chain dialogue. They are outside of OnAiTalk, which makes them a bit more flexible as I mentioned above.
//Each chain function will go through each part of the chain in order even if it's in one function, until it goes to \e:chain=end which will end the chain, as you could probably guess. So you can put the entire chain dialogue into one place.
//You don't have to make chains if you don't want to! This is just for reference if you ever have a conversation come up where it could be handy.

//achain
//{{CHAIN
	//this will happen the first time the chain is called
	//"\0\s[0]Here is the second part of the chain.\w8\1\s[10]Will there be more?\e"
	
	//they will then go to this dialogue next
	//"\0\s[0]This is the third part of chained dialogue.\1\w8\s[10]This will lead to a fourth part.\e"
	
	//this will be the last piece of dialogue in the chain, then they will go back to their normal dialogue
	//"\0\s[0]This is the fourth piece of chained dialogue.\1\w8\s[10]This is the end.\e:chain=end"
//}}CHAIN

//Pay careful attention to the CHAIN text and double brackets for these. Double brackets, CHAIN, dialogue, double brackets, CHAIN. If you are planning on making chains, I'd recommend just copying and pasting the chain example here and changing the dialogue and name of the function as you need. Less likely for mistakes to happen, you know?
//Note also it ends with \e:chain=end, basically ending the chain.

rickrollchain
{{CHAIN
	"\0\s[0]... \w8\s[6]What? \w4Did you think I would do something?\0\w8\n\n[half]\s[5]Hehe, \w6nah, \w4I'm a good boy!\e:chain=end"
}}CHAIN

internetchain
{{CHAIN
	"\0\s[5]Welcome to the internet. \w8\s[6]Have a look around!\w4\1\s[18]Huh? \w2\0\n\s[21]Anything that \1\s[10]\0\s[21]brain of yours can think of can be found...\w2\n\n\s[6]We've got mountains of content.\w1\1\s[13]Um...\w1\0\n\n[half]\s[22]Some better, \w1\s[23]some worse! \n\n\w8\s[8]If none of it's of interesest to you, \w1\s[20]you'd be the first!\w8\w4\n\n[half]\s[5]Welcome to the internet!\w1\1\s[51]\0\s[6] Come and take a seat...\w1\n\nWould you like to see the news \s[20]or any famous womens feet?\w1\1\s[14] I..\0\n\s[22]There's no need to panic! \w3\s[21]This isnt a test... \n\n\w1\s[6]Heh, \w6\s[5]Just nod or shake your head and \s[21]we'll do the rest!\w4\1\s[10]We?- \w1\0\n\n\s[5]\1\s[12]\0\s[5]\f[height,20]Welcome!\f[height,default]\1\s[10]\0\s[0] to the internet! \w4\s[5]What would you prefer? \w1\1\s[51]\0\n\n\s[6]Would you like to fight for civil rights \1\s[14]\0\s[20]or tweet a racial slur?\w1\1\n\nWhat??? \0\n\n\s[5]Be happy! \s[20]Be horny! \w2\s[7]Be burning with RAGE!\1\s[14]God help me..\w1\0\s[5] We got a million different ways to engage!\e"
	
	"\0\s[6]\0\s[6]Welcome to the internet! \w8\s[20]Put your cares aside! \w4\n\n[half]\s[22]Here's a tip for straining pasta, \s[23]here's a 9 year old who died!\1\s[14]Oh my god... \0\n\s[21]We've got movies, \w2\s[20]and doctors, \w2\s[5]and fantasy spoooorts!! \w2\n\n\s[0]And a bunch of coloured pencil drawings of a \1\s[13]\0\s[0]bunch of the different characters in Harry Potter doing the do \f[height,20]\1\s[12]\0\s[5]WELCOME TO THE \1\s[51]\0\s[5]INTERNET!\f[height,default]\w4\n\n\s[0]Hold on to your socks, \w2\s[20]cus a random guy just kindly sent you photos of his \s[3]\f[height,15]\f[bold,true]\1\s[12]\0\s[3]*HOT DOG.*\f[bold,false]\f[height,default]\w1\n\n\s[3] They are grainy, \w1and offputting... \w1\s[1]He just sent you mooooore!\w1\1\s[10] \w2\1\s[12]\0\s[5] Don't act surprised, \s[20]you know \1\s[19]\0\s[20]you like it, \w1 you \f[height,15]\f[bold,true]*HOGDOG LOVER*!\f[height,default]\f[bold,false] \w4\1\n\nI'm ignoring you.  \e"
	
	"\0\s[5]See a man beheaded, get offended see a shrink, \n\n[half]\s[22]Show us pictures of your children, \s[23]tell us every thought you think! \n\s[22]Start a rumor, \s[23]buy a broom \s[21]or send a death threat to a boomer \n \s[22]Or DM a girl and groom her, \s[23]do a zoom or find a tumor in your- \n\n[half]\s[5]Here's a healthy breakfast option!\n\s[21]You should kill your mom! \n\s[22]Here's why women never *FUN TIME* with you!\n\s[23]Here's how you can build a bomb!\n\n[half]\s[1]Which Power Ranger are you?\w4\s[20] Take this quirky quiz! \s[22]Obama sent\s[23] the immig\s[22]rants to vaccin\s[23]ate your kids\s[22]!\w9\w9\w9\1\s[19]...\w2 Are you don-\1\s[12]\0\n\n[half]\s[5] Can I int\1\s[19]\0\s[5]erest you in everything, \w2\s[20]all of the time? \w4\s[6]\nA little bit of everything, \w2all of the time? \s[22]\w4\n\nApathy's a tragedy and \w1\s[23]bordom is a crime. \w5\n\n\s[20]Anything and everything, \w3\s[21]all of the time?\w4\s[0]\w9\w9\1\s[10]\n\nDo you want me to applaud you or something?\w8\1\s[19]\0\s[6]\n\nYes!\e:chain=end"
}}CHAIN

glowingchain
{{CHAIN
	"\1\s[13]That place always gave me the creeps anyway... \0\s[0]\1\w4\n\n\s[18]There was always something weird going on when I came to visit.\0\w8\s[2]Seriously? \n\n\w4\s[3]I didn't think there was anything wrong with it...\1\n\n\s[19]Well aren't you lucky? \n\n\s[16]\w8Pretty sure I saw \_a[https://www.youtube.com/watch?v=fIT1RSAcTDo]something glowing\_a in your room one of those days.\0\n\n[half]\s[2]\f[height,20]\f[bold,true]What!?\e"
	
	"\0\s[7]Well,\w2 those stairs deserved to get slammed on by my face anyways!\w8\1\s[16]Uh huh?\w4\0\s[7]\n\nYeah!!! \s[4]\nI don't trust stairs...\1\s[10]\0\w8\s[6] they're always\w4\f[bold,true] up\f[bold,false]\w4 to something, heh.\w9\w9\s[0]\n\n...\w8\s[2]Uh-! Get it?? Cus like-\w1\1\s[19]\n\n Yea,\w2 I got it.\w4\nIt wasn't funny.\w5\0\s[1]\n\nGUH-\w4 \1\s[16]\0\WUH-\w8\s[7]\n \f[bold,true]SCOFFFFF!!!\f[bold,false]\e:chain=end"
}}CHAIN

kisschain
{{CHAIN
	"\0\s[6]\_a[https://www.youtube.com/watch?v=rsF5EWOQN9Y]Kiss me~\_a\w8\1\s[10]What?\w4\0\s[2]\n\n\f[height,20]\f[bold,true]Uhh-!\f[height,default]\f[bold,false]\w8\s[4] cus \w2like...\1\s[19]\w8\0\s[4] you said- \w8\w4\n\nuuhhhh... \w8\w4\s[9]nevermind.\e:chain=end"
}}CHAIN

mentalhealthchain
{{CHAIN
	"\1\s[13]...\w8\s[10] You can't just believe your depression away, y'know?\w8\0\s[6] I hear you, Mr. Psychiatrist,\w4 but consider...\w8\w8\n\n\s[8] What if...\w6\s[5] I did it anyways?\w8\1\n\n...\w8\s[19] I'm not doing this with you right now.\e:chain-end"
}}CHAIN

timeloopchain
{{CHAIN
	"\0\s[0]\1\s[16]Obviously,\w2 the REAL reason you shouldn't invest in time travel is so you don't accidently trap yourself in a time loop.\w8\0\s[9] I mean, \w2I GUESS... \w4\1\s[18]\n\nSeriously.\w4 Loosing all you once knew,\0\s[0]\1\w2 repeating the same events over and over again until they become like a play in your head,\w4 family and friends reduced to actors on a stage.\n\s[10]Your life has become nothing more than a \0\s[3]\1reppeating pattern. \w4\n\n\s[18]Sure, you could try to switch things up.\w4 Do things differently, \w2act differently,\0\s[4]\1 \w4maybe try to...\w4 change the course of history, or whatever, but it won't REALLY change anything.\w5\n\n\s[10]Sad existence, \w2 really.\w8\0\s[4]\n\nThat's a...\w4 very specific description.\w8\1\s[10]\w8\w9\1\s[16]\n\nI'm just well versed in these things.\w8\e"
}}CHAIN

weirdhatchain
{{CHAIN
	"\1\s[14]...\w9\w9\s[13]It's nothing important,\w4 I guess...\e"
}}CHAIN
//You can make chains as long or as shot as you want! Just make sure to end the chain when you're done.


//Alright, we're done with the random conversations for now, but there are a few other things in aitalk that we'll need to fill out too.


//-OnMinuteChange
//These are time-related bits of dialogue for your ghost to say. If any of them look interesting, uncomment them! I commented them out for simplicity's sake, but it's up to you if you want to use them or not!

//OnMinuteChange
//{
//HourCheck
//--
//if mikireflag == 1
// 	{
// 		MikireTalk
//	}
//}


//Keeps track of if an hour has passed. Just ignore this.
HourCheck
{
if passmin >= 59
	{
		passmin = 0
		passhour = passhour + 1
	}
}


OnMinuteChange //This is what will trigger time-based dialogue for your Ghost.
 {
	HourCheck //checks to see if an hour has passed, ignore it
	--
 	 if minute == 0 //These ifs will check what time it is. Here, it's seeing if the minute equals zero...
	 {
 		 if hour == 12 //And the hour equals 12, meaning it's noon. You can change this to some other hour if you want.
		 {
 			 "\1\s[10]It's noon.\w8\0\s[5]Let's have a showdown!\w8\1\n\n[half]\s[19]There are no corrals here, dude.\e"
			//Replace with appropriate dialogue as you want.
		 }
 		 elseif hour == 0 //If the hour is zero, aka midnight. Again, you can change this to whatever hour you want to check for. This section is flexible! You can also add more elseifs if you want to check for other hours.
		 {
 			 "\0\s[2]Oh wow, it's midnight!\w8\1\s[10]Boo.\w2\0\n\s[1]\f[height,20]\f[bold,true]Waaaaaaaaaaaaahh!!\f[height,default]\e"
			 
 			 "\1\s[18]It's getting late.\w8\0\s[5]Are you scaaaaaaaaaaared?\w8\1\n\s[19]I think \0\s[0]\1\s[19]\w2\f[bold,true]you're \f[bold,false]\w4scared.\w8\w6\0\n\s[0]...\w8\0\s[4]\e"
			 
 			 "\1\s[18]Why are you still up at midnight?\w8\0\s[7]We don't \1\s[19]\0\s[7]question %(username)'s sleeping habits, Jelicream!\w8\w8\1\n\n[half]\s[18]Go to sleep.\e"
			// //Then replace the dialogue with what's appropriate.
		 }
		 else
		 {
			 "\0\s[0]Hey, it's %(hour12)'o clock!\e"
			// //%(hour12) stores the time. This is a general top of the hour notification. I personally like this because it helps me keep track of time when I've been working on something, so when a ghost is all "Hey it's 3 o clock" I can be like woah really when did that happen. If you think this is annoying though, you can just delete this bit of dialogue to leave this else statement blank, or leave "" in here instead.
		 }
	// }
	// //You can also check for more specific times! For example...
	// elseif hour == 23 && minute == 55 //this checks to see if the hour is 23 and the minutes are at 55... essentially, 11:55 pm.
	// {
		// "\0\s[0]5 minutes to midnight.\e"
		// //Replace with what you like!
	// }
	// elseif hour == 7 && minute == 30 && weekday == 0 //Here, it's checking for a specific time on a specific day. You can add things to your time checks just like this by using the AND operator, && (more about operators at the walkthrough page). This adds up to 7:30 am, Sunday. If you have a favorite TV program on at some time, you could have your ghost comment on when it comes on like this.
	// {			
		// "\0\s[0]It's 7:30 on Sunday.\e"
		// //Replace with what's appropriate.
	 }
	// //If you're not interested in these more specific checks, you can delete those last two elseifs and just leave the one for minute==0, or clear them all out as mentioned above, although leave in the HourCheck and mikireflag bits.
	
	// 
	
	
	//Advanced users who want to enable the off-screen boundary check = Remember mikireflag up in OnAiTalk? This is the flag that checks for that. You'll want to uncomment this here so it can trigger the dialogue.
	
	//Don't touch this part.
	// elseif mikireflag == 1
 	// {
 		// MikireTalk
	// }
 }

//--OnSecondChange
//Like the above, this handles what happens every time a second goes by. Most of this you will not have to touch.

OnSecondChange
 {
	if passsec == 59
 		{
	 	passsec = 0
 		passmin = passmin + 1
		}
 	else
 		{
	 	passsec = passsec + 1
		}
		
		
	//Advanced users - these next bits are related to the mikireflag and when a character is off screen. Uncomment these if you want to use this feature. 
		
	// if mikireflag == 0 && reference1 == 1 //Dialogue for putting them off the screen. You could make this a unique line if you wanted (Like "Hey! What are you doing?"), and have MikireTalk below be for if they continue to be offscreen ("Why are you leaving me out here? Help!" for example). I'll add an example bit of dialogue and comment it out, you can uncomment it and use it if you like, or just leave it out and use MikireTalk below.
	// {
		// //"\0\s[0]You've moved me off screen."
		
		// MikireTalk
	// }
	// if mikireflag == 1 && reference1 == 0 //Dialogue for coming back on the screen
	// {
		// EndMikireTalk
	// }
	// mikireflag = reference1 //reference1 is if the ghost is offscreen or not
	
	
	//This dialogue is for when the two characters are overlapping each other. Uncomment this if you want to have dialogue for it!
	
	// if reference2 == 1
	// {
 		// "\0\s[0]Me and Triangle are overlapping.\w8\1\s[19]That's strange since your resolution is %screenwidthx%screenheight, %(username). Shouldn't we have enough space?\4\e"
		
		// //You can see here the use of two envelopes, screenwidth and screenheight, to tell the user their resolution. Oddly, they are not in parentheses. I'm not sure why this is, but they don't display right otherwise. An odd quirk with these two envelopes.
		// //Note also how Triangle moves away at the end of the dialogue. This is due to the \4 tag. That will make any one character move away from the other if they are able. I talked about this tag and its counterpart tag \5 in the SakuraScript page on the walkthrough.
	// }
 }

 //---- MikireTalk ---------------------------------------------------------------------
 //MikireTalk is dialogue for when your main character is off-screen. Like if you dragged Girl over to the side of your monitor until she was partially concealed by it. Advanced users, make sure to uncomment this along with all the other mikire related things in here I've pointed out.
 
// MikireTalk
 // {
	// "\0\s[0]I'm off the screen.\w8\1\s[10]You can push Ctrl-Shift-F12 to bring her back."
	// //Note the hotkey combo Triangle mentions. He's telling the truth, that should bring her back on the screen.
 // }

 //----EndMikireTalk--------------------------------------------------------------------
 //For after you bring the character back on screen.
 
// EndMikireTalk
 // {
	// "\0\s[0]I'm no longer off the screen.\w8\1\s[19]What a shame.\w8\0\s[9]\n\n[half]Shut up.\e"
 // }


//**** OnSurfaceRestore *****************************************************************
//OnSurfaceRestore is a handy function. What this does is that after a short period of time, the ghost will go back to its default pose, so you won't have a ghost stuck in one expression for too long. It's also helpful if you've made one of the characters disappear using \s[-1] in dialogue and didn't bring them back at the end of it. This will do that for you.
//I'm going to comment out the if/else statements in this because the circumstances in which they'd come up seem pretty unlikely and unnecessary if you're just going to be making a simple ghost. You can just leave this function alone and move on if you want. However, if you do want to use them, just uncomment the lines by deleting the slashes and fill in the info as you need.

//*********************Advanced User Info*******************************
//You can put if statements in OnSurfaceRestore to make exceptions for certain poses, or to make sure that once they're in a certain pose, they stay in that pose. For example, if you turn my Hunter and Smoker ghost into eggplants, there's a check for the their eggplant pose numbers to make sure they STAY eggplants when OnSurfaceRestore is called.
//You can also use OnSurfaceRestore to clean up things that might be left on the screen, primarily from using p[number]\s[] to call up other objects or characters. Just make sure to add a p[number]\s[-1] to OnSurfaceRestore, and that should get rid of it each time the ghost cleans itself up.
//***********************************************************************

OnSurfaceRestore
 {
 	//if reference0 == 4 || reference0 == 6 //This is checking what poses they are in. This looks to see if Girl is in pose 4 or pose 6, since she is reference0.
 	//{
 	//"\1\s[10]You're in a sad pose.\w8\0\s[0]I'll go back to normal then.\e"
 	//}
 	//elseif reference1 == 14 || reference1 == 16 //This is doing the same but for Triangle, since he's reference1. It looks to see if he's in pose 14 or post 16.
 	//{
 	//"\0\s[0]You look sad.\w8\1\s[10]I'll go back to normal.\e"
 	//}
 	//else
 	//{
	"\0\s[0]\1\s[10]\e"
	//}
 }

 
 //--OnKeyPress
 //This defines the hotkeys your ghost will use. You can change these to whatever you want, but I don't really know why you would. I'd just leave this alone. Advanced Users can add new hotkeys attached to whatever function if you want.
 
OnKeyPress
{
	if reference0 == "t"
	{
	OnAiTalk
	}
	elseif reference0 == "r"
	{
	OnLastTalk
	}
	elseif reference0 == "f1"
	{
	"\![open,readme]"
	}
}